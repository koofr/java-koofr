plugins {
    id 'java-library'
    id 'maven-publish'
}

version = '3.6.0'

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.eclipsesource.minimal-json:minimal-json:0.9.4'
}

Properties local = new Properties()
local.load(project.rootProject.file("local.properties").newDataInputStream())

publishing {
    repositories {
        maven {
            name = 'koofr'
            url = local.getProperty('repo.url')
            credentials {
                username = local.getProperty('repo.user')
                password = local.getProperty('repo.key')
            }
        }
    }
    publications {
        koofr(MavenPublication) {
            groupId = 'net.koofr'
            from(components.java)
            pom {
                name = 'java-koofr'
                description = 'A Java client for Koofr storage service API'
                url = 'https://github.com/koofr/java-koofr'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://github.com/koofr/java-koofr/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'jkmcnk'
                        name = 'Jaka Moƒçnik'
                        email = 'jaka@koofr.net'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/koofr/java-koofr.git'
                    developerConnection = 'scm:git:https://github.com/koofr/java-koofr.git'
                    url = 'https://github.com/koofr/java-koofr'
                }
            }
        }
    }
}
